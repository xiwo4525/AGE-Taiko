.版本 2

.程序集 灯光MOD
.程序集变量 遮罩图片, Tp_图片
.程序集变量 遮罩精灵, Age_精灵
.程序集变量 背景图片, Tp_图片
.程序集变量 背景精灵, Age_精灵
.程序集变量 黑精灵, Age_精灵
.程序集变量 灯光渲染区, Tp_渲染区
.程序集变量 灯光纹理, 整数型
.程序集变量 灯光精灵, Age_精灵
.程序集变量 灯光值, 整数型
.程序集变量 缩放值, 小数型
.程序集变量 缩放设定值, 小数型

.子程序 灯光初始化

遮罩图片.纹理 ＝ 引擎.载入纹理 (“灯光.png”)
遮罩精灵.创建 (遮罩图片, 0, 0, 引擎.取图片宽 (遮罩图片), 引擎.取图片高 (遮罩图片))
遮罩精灵.置中心 (引擎.取图片宽 (遮罩图片) ÷ 2, 引擎.取图片高 (遮罩图片) ÷ 2)
黑精灵.创建矩形 (0, 0, 分辨率.窗口宽度, 分辨率.窗口高度, #颜色_黑)
灯光渲染区.句柄 ＝ 引擎.创建渲染区 (分辨率.窗口宽度, 分辨率.窗口高度, 假)
灯光纹理 ＝ 引擎.取渲染区纹理 (灯光渲染区)
灯光精灵.创建_从指针 (灯光纹理, 0, 0, 分辨率.窗口宽度, 分辨率.窗口高度)
灯光值 ＝ 0
缩放设定值 ＝ 3

.子程序 灯光渲染区渲染

引擎.渲染开始 (灯光渲染区)
引擎.清屏 ()

黑精灵.置混合模式 (#混合_默认)
黑精灵.置颜色 (ARGB (255, 灯光值, 灯光值, 灯光值))
黑精灵.显示 ()
遮罩精灵.置混合模式 (#混合_SrcColorAdd)
遮罩精灵.显示高级 (250, 230, 0, 缩放值, 缩放值)

引擎.渲染结束 ()

.子程序 灯光渲染区遗失

灯光精灵.置纹理_指针 (引擎.取渲染区纹理 (灯光渲染区))

.子程序 灯光显示

灯光精灵.置混合模式 (#混合_乘)
灯光精灵.显示 ()

.子程序 设定缩放
.参数 缩放, 小数型

缩放设定值 ＝ 缩放

.子程序 调整缩放值

.如果 (缩放值 ≤ 缩放设定值)
    .如果 (缩放值 ≥ 缩放设定值)
        缩放值 ＝ 缩放设定值
    .否则
        缩放值 ＝ 缩放值 ＋ 0.05
    .如果结束

.否则
    .如果 (缩放值 ≤ 缩放设定值)
        缩放值 ＝ 缩放设定值
    .否则
        缩放值 ＝ 缩放值 － 0.05
    .如果结束


.如果结束

